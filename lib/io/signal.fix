// Unix signal handling
module Minilib.IO.Signal;

import Minilib.IO.Errno;
import Minilib.Monad.IO;

// An array of supported signal names.
signal_names: Array String;
signal_names = [
    "SIGHUP",
    "SIGINT",
    "SIGQUIT",
    "SIGILL",
    "SIGTRAP",
    "SIGABRT",
    "SIGIOT",
    "SIGBUS",
    "SIGFPE",
    "SIGKILL",
    "SIGUSR1",
    "SIGSEGV",
    "SIGUSR2",
    "SIGPIPE",
    "SIGALRM",
    "SIGTERM",
    "SIGSTKFLT",
    "SIGCHLD",
    "SIGCONT",
    "SIGSTOP",
    "SIGTSTP",
    "SIGTTIN",
    "SIGTTOU",
    "SIGURG",
    "SIGXCPU",
    "SIGXFSZ",
    "SIGVTALRM",
    "SIGPROF",
    "SIGWINCH",
    "SIGIO",
    "SIGPWR",
    "SIGSYS",
];

// An array of supported signal handler names.
sighandler_names: Array String;
sighandler_names = [
    "SIG_IGN",
    "SIG_DFL"
];

// `set_signal_handler(signal_name, sighandle_name)` sets the handler for the signal.
// `signal_name` should be one of `signal_names`.
// `sighandle_name` should be one of `sighandler_names`.
set_signal_handler: [m: MonadIOFail] String -> String -> m ();
set_signal_handler = |signal_name, sighandler_name| lift_iofail $ do {
    let res = *signal_name.borrow_c_str_io(|p_signal_name|
        sighandler_name.borrow_c_str_io(|p_sighandler_name|
            FFI_CALL_IO[CInt minilib_io_signal_set_signal_handler(Ptr, Ptr), p_signal_name, p_sighandler_name]
        )
    ).lift;
    if res.to_I64 != 0 {
        throw $ "set_signal_handler(\"" + signal_name + "\",\"" + sighandler_name + "\") failed!: " + *get_last_error
    };
    pure()
};

// `kill_process(pid, signal_name)` sends a signal to the process specified by `pid`.
// `signal_name` should be one of `signal_names`.
//
// For details, see see Linux manual page for [kill(2)](https://man7.org/linux/man-pages/man2/kill.2.html).
kill_process: [m: MonadIOFail] I64 -> String -> m ();
kill_process = |pid, signal_name| lift_iofail $ do {
    let res = *signal_name.borrow_c_str_io(|p_signal_name|
        FFI_CALL_IO[CInt minilib_io_signal_kill_process(CInt, Ptr), pid.to_CInt, p_signal_name]
    ).lift;
    if res.to_I64 != 0 {
        throw $ "kill_process(" + pid.to_string + ") failed!: " + *get_last_error
    };
    pure()
};
