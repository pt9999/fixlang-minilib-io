// File system module. For example, finding files, checks if file or directory exists,
// getting file size and last modified time.
module Minilib.IO.FileSystem;

import Time;

import Minilib.Encoding.Binary;
import Minilib.IO.Errno;
import Minilib.IO.Platform;
import Minilib.IO.Path;
import Minilib.Text.StringEx;

//--------------------------------------------------------------------------
//  File status
//--------------------------------------------------------------------------

// Returns true if the specified file exists.
file_exists: String -> IO Bool;
file_exists = |file_path| (
    let res = *stat(file_path).to_result;
    if res.is_err {
        pure $ false
    };
    let file_stat = res.as_ok;
    pure $ file_stat.is_file
);

// Returns true if the specified directory exists.
directory_exists: String -> IO Bool;
directory_exists = |dir_path| (
    let res = *stat(dir_path).to_result;
    if res.is_err {
        pure $ false
    };
    let file_stat = res.as_ok;
    pure $ file_stat.is_dir
);


// Type of file status
type FileStat = unbox struct {
    data: Array U64
};

namespace FileStat {
    // `stat(file_path)` retrieves information about the file pointed to by `file_path`.
    stat: String -> IOFail FileStat;
    stat = |file_path| (
        pure();; // make it lazy
        clear_errno.lift;;
        let data = Array::fill(16, 0_U64);
        let (data, res) = *data.mutate_boxed_io(|p_data|
            file_path.borrow_c_str_io(|p_filepath|
                FFI_CALL_IO[CInt minilib_iofs_stat(Ptr, Ptr), p_filepath, p_data]
            )
        ).lift;
        if res != 0.to_CInt {
            throw("stat failed!: " + *get_last_error.lift)
        };
        pure $ FileStat { data: data }
    );

    // Returns true if it is a regular file.
    is_file: FileStat -> Bool;
    is_file = |file_stat| (
        FFI_CALL[CInt minilib_iofs_stat_mode_is_file(U32), file_stat.st_mode]
        != 0.to_CInt
    );

    // Returns true if it is a directory.
    is_dir: FileStat -> Bool;
    is_dir = |file_stat| (
        FFI_CALL[CInt minilib_iofs_stat_mode_is_dir(U32), file_stat.st_mode]
        != 0.to_CInt
    );

    st_dev: FileStat -> U64;
    st_dev = |file_stat| file_stat.@data.@(0);

    st_ino: FileStat -> U64;
    st_ino = |file_stat| file_stat.@data.@(1);

    st_nlink: FileStat -> U64;
    st_nlink = |file_stat| file_stat.@data.@(2);

    st_mode: FileStat -> U32;
    st_mode = |file_stat| file_stat.@data.@(3).to_U32;

    st_uid: FileStat -> U32;
    st_uid = |file_stat| file_stat.@data.@(4).to_U32;

    st_gid: FileStat -> U32;
    st_gid = |file_stat| file_stat.@data.@(5).to_U32;

    st_rdev: FileStat -> U64;
    st_rdev = |file_stat| file_stat.@data.@(6);

    st_size: FileStat -> I64;
    st_size = |file_stat| file_stat.@data.@(7).to_I64;

    st_blksize: FileStat -> I64;
    st_blksize = |file_stat| file_stat.@data.@(8).to_I64;

    st_blocks: FileStat -> U64;
    st_blocks = |file_stat| file_stat.@data.@(9);

    st_atim: FileStat -> Time;
    st_atim = |file_stat| Time {
        sec: file_stat.@data.@(10).to_I64,
        nanosec: file_stat.@data.@(11).to_U32,
    };

    st_mtim: FileStat -> Time;
    st_mtim = |file_stat| Time {
        sec: file_stat.@data.@(12).to_I64,
        nanosec: file_stat.@data.@(13).to_U32,
    };

    st_ctim: FileStat -> Time;
    st_ctim = |file_stat| Time {
        sec: file_stat.@data.@(14).to_I64,
        nanosec: file_stat.@data.@(15).to_U32,
    };

    st_atime: FileStat -> U64;
    st_atime = |file_stat| file_stat.@data.@(10);

    st_mtime: FileStat -> U64;
    st_mtime = |file_stat| file_stat.@data.@(12);

    st_ctime: FileStat -> U64;
    st_ctime = |file_stat| file_stat.@data.@(14);
}

//--------------------------------------------------------------------------
//  Finding files
//--------------------------------------------------------------------------

// `find_files(dir_path)` finds all files under
// specified directory and its subdirectories.
find_files: String -> IOFail (Array String);
find_files = |dir_path| (
    let dir_path = if dir_path.ends_with("/") { dir_path } else { dir_path + "/" };
    let output = Array::empty(10);
    let files = *list_dir(dir_path);
    files.to_iter.loop_iter_m(
        output, |file, output|
        let file_path = dir_path + file;
        if *directory_exists(file_path).lift {
            let output = output.append(*find_files(file_path));
            continue_m $ output
        };
        if *file_exists(file_path).lift {
            let output = output.push_back(file_path);
            continue_m $ output
        };
        continue_m $ output
    )
);

// Lists a directory.
// Returns filenames in the specified directory.
// The filenames will be sorted in lexicographical order.
list_dir: String -> IOFail (Array String);
list_dir = |dirpath| (
    pure();;    // make it lazy
    let dir_handle = *_opendir(dirpath);
    let names: Array String = Array::empty(10);
    let names = *loop_m(
        names, |names|
        let name = *_readdir(dir_handle);
        if name == "" {
            break_m $ names
        };
        if name == "." || name == ".." {
            continue_m $ names
        };
        let names = names.push_back(name);
        continue_m $ names
    );
    let names = names.sort_by(|(a,b)| a < b);
    pure $ names
);

// [nofixdoc] Type of a directory handle
type DirHandle = unbox struct {
    dtor: Destructor Ptr
};

_opendir: String -> IOFail DirHandle;
_opendir = |dir_path| (
    clear_errno.lift;;
    let dirp = *dir_path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[Ptr opendir(Ptr), p_path]
    ).lift;
    if dirp == nullptr {
        throw("opendir failed!: " + *get_last_error.lift
                + "\ndir_path=" + dir_path)
    };
    pure $ DirHandle {
        dtor: Destructor::make(dirp, |dirp|
            //eval debug_eprintln ("closing dir handle");
            //eval clear_errno._unsafe_perform;
            let res = *FFI_CALL_IO[I32 closedir(Ptr), dirp];
            /*eval if res != 0_I32 {
                debug_eprintln("closedir failed!: res=" + res.to_string)
            } else {
                ()
            };
            */
            pure $ nullptr
        )
    }
);

_readdir: DirHandle -> IOFail String;
_readdir = |dir_handle| (
    clear_errno.lift;;
    let bufsize = 256;
    let buf = Array::fill(bufsize, 0_U8);
    let (buf, len) = *dir_handle.@dtor.borrow_io(|dirp|
        buf.mutate_boxed_io(|p_buf|
            FFI_CALL_IO[CSizeT minilib_iofs_readdir(Ptr, Ptr, CSizeT), dirp, p_buf, bufsize.to_CSizeT]
        )
    ).lift;
    if len.to_I64 == 0 {
        let errno = *get_errno.lift;
        if errno != 0_I32 {
            throw("readdir failed!: " + *get_last_error.lift)
        };
        pure $ ""
    };
    let buf = buf.get_sub(0, len.to_I64);
    pure $ buf._unsafe_to_string
);

//--------------------------------------------------------------------------
//  Directory
//--------------------------------------------------------------------------

// `make_dirs(dir_path, mode)` creates specified directory
// as well as its parent directories recursively.
// If the directory already exists, it does nothing.
// If `mode` is `none()`, octal 0777 is used as a mode.
// This mode is modified by the process's umask in the usual way.
make_dirs: String -> Option U32 -> IOFail ();
make_dirs = |dir_path, mode| (
    if *directory_exists(dir_path).lift {
        pure()
    };
    let parent_dir = dirname(dir_path);
    let _ = *if parent_dir == dir_path {
        pure()  // avoid infinite loop
    } else {
        make_dirs(parent_dir, mode)
    };
    mkdir(dir_path, mode)
);

// `mkdir(path, mode)` creates a directory.
// If `mode` is `none()`, octal 0777 is used as a mode.
// This mode is modified by the process's umask in the usual way.
mkdir: String -> Option U32 -> IOFail ();
mkdir = |path, mode| (
    clear_errno.lift;;
    let mode = if mode.is_some { mode.as_some } else { /* 0777 = */ 511_U32 };
    let res = *path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[I32 mkdir(Ptr, U32), p_path, mode]
    ).lift;
    if res != 0_I32 {
        throw("mkdir failed!: " + *get_last_error.lift)
    };
    pure()
);

// `rmdir(path)` deletes a directory, which must be empty.
rmdir: String -> IOFail ();
rmdir = |path| (
    clear_errno.lift;;
    let res = *path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[I32 rmdir(Ptr), p_path]
    ).lift;
    if res != 0_I32 {
        throw("rmdir failed!: " + *get_last_error.lift)
    };
    pure()
);


//--------------------------------------------------------------------------
//  Files
//--------------------------------------------------------------------------

// Flushes a file stream.
// For details, see Linux manual page for [fflush()](https://man7.org/linux/man-pages/man3/fflush.3.html).
fflush : IOHandle -> IOFail ();
fflush = |handle| (
    clear_errno.lift;;
    let res = *FFI_CALL_IO[I32 fflush(Ptr), handle._file_ptr].lift;
    if res < 0_I32 {
        throw("fflush failed!: " + *get_last_error.lift)
    };
    pure()
);

// Sets IOHandle to unbuffered mode.
// For detials, see Linux manual page for [setbuf()](https://man7.org/linux/man-pages/man3/setbuf.3.html).
// NOTE: When a fix program is invoked by `run_with_stream()`,
//       then the stdout and stderr becomes not a TTY but a file stream.
//       So the stdout becomes block-buffered. The stderr also seems to be block-buffered.
set_unbuffered_mode: IOHandle -> IO ();
set_unbuffered_mode = |handle| (
    pure();;
    FFI_CALL_IO[() setbuf(Ptr, Ptr), handle._file_ptr, nullptr]
);

// Returns the canonicalized absolute pathname.
// For detials, see Linux manual page for [realpath()](https://man7.org/linux/man-pages/man3/realpath.3.html).
realpath: String -> IOFail String;
realpath = |path| (
    let p_resolved: Ptr = *path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[Ptr realpath(Ptr, Ptr), p_path, nullptr]
    ).lift;
    if p_resolved == nullptr {
        throw("realpath failed!: " + *get_last_error.lift + ": path=" + path)
    };
    let resolved: String = String::_unsafe_from_c_str_ptr(p_resolved);
    FFI_CALL_IO[() free(Ptr), p_resolved].lift;;
    pure $ resolved
);

// Creates a new file or rewrites an existing one.
// For details, see Linux manual page for [creat()](https://man7.org/linux/man-pages/man3/creat.3p.html).
creat: String -> U32 -> IOFail I32;
creat = |path, mode| (
    pure();;    // make it lazy
    clear_errno.lift;;
    let fd = *path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[I32 creat(Ptr, U32), p_path, mode]
    ).lift;
    if fd == -1_I32 {
        throw("creat failed!: " + *get_last_error.lift)
    };
    pure $ fd
);

// Deletes a name from the filesystem and possibly the file it refers to.
// For details, see Linux manual page for [unlink()](https://man7.org/linux/man-pages/man2/unlink.2.html).
unlink: String -> IOFail ();
unlink = |path| (
    pure();;    // make it lazy
    clear_errno.lift;;
    /*
    let res = *(IO::from_runner $ |ios|
        path.borrow_c_str(|p_path|
            FFI_CALL_IOS[I32 unlink(Ptr), p_path, ios]
        )
    ).lift;
    */
    let res = *path.borrow_c_str_io(|p_path|
        FFI_CALL_IO[I32 unlink(Ptr), p_path]
    ).lift;
    if res == -1_I32 {
        throw("unlink failed!: " + *get_last_error.lift)
    };
    pure()
);

// Associates a stream with a file descriptor.
// For details, see Linux manual page for [fdopen()](https://man7.org/linux/man-pages/man3/fdopen.3p.html).
fdopen: I32 -> String -> IOFail IOHandle;
fdopen = |fd, mode| (
    pure();;    // make it lazy
    clear_errno.lift;;
    let file_ptr = *mode.borrow_c_str_io(|p_mode|
        FFI_CALL_IO[Ptr fdopen(I32, Ptr), fd, p_mode]
    ).lift;
    if file_ptr == nullptr {
        throw("fdopen failed!: " + *get_last_error.lift)
    };
    pure $ IOHandle::from_file_ptr $ file_ptr
);


// Creates a pipe. It returns `(read_fd, write_fd)` where `read_fd` is the file descriptor of
// read-end of the pipe, and `write_fd` is the file descriptor of write-end of the pipe.
// For details, see Linux manual page for [pipe()](https://man7.org/linux/man-pages/man2/pipe.2.html).
pipe: IOFail (I32, I32);
pipe = (
    pure();;    // make it lazy
    clear_errno.lift;;
    let pipefd: Array I32 = Array::fill(2, 0_I32);
    let res = *pipefd.borrow_boxed_io(|p_pipefd|
        FFI_CALL_IO[I32 pipe(Ptr), p_pipefd]
    ).lift;
    if res == -1_I32 {
        throw("pipe failed!: " + *get_last_error.lift)
    };
    let read_fd = pipefd.@(0);
    let write_fd = pipefd.@(1);
    pure $ (read_fd, write_fd)
);

// Creates a pipe stream. It returns `(read_fh, write_fh)` where `read_fd` is the stream of
// read-end of the pipe, and `write_fd` is the stream of write-end of the pipe.
// For details, see Linux manual page for [pipe()](https://man7.org/linux/man-pages/man2/pipe.2.html).
open_pipe: IOFail (IOHandle, IOHandle);
open_pipe = (
    let (read_fd, write_fd) = *pipe;
    let read_fh = *fdopen(read_fd, "r");
    let write_fh = *fdopen(write_fd, "w");
    pure $ (read_fh, write_fh)
);
